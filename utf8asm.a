;ACME 0.97

; welche Fehler sind denkbar?
;	-unerwartete Fortsetzungsbytes (zuviele bzw. ganz ohne Startbyte)
;	-fehlende Fortsetzungsbytes
;	-verbotene Bytes (0xc0, 0xc1, 0xf8+)

	!to "utf8asm.boot", cbm
	!ct pet
	* = $1300

	CHAR_CODING_ERROR	= '!'	; char to output in case of encoding error
	CHAR_UNSUPPORTED	= 'X'	; char to output for unsupported code points

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; UTF-8 decoder

; first entry point: init / reset / clear error flag
		jmp .init
; second entry point: feed a byte to decoder
		;jmp decode_byte_A
;decode_byte_A
		tax	; check msb and buffer original value so we can shift A around
		bpl .ascii_AX	; handle ascii value (0x00..0x7f)
		asl
		bpl .sixbits_X	; handle continuation byte (0x80..0xbf)
		ldy #1	; minimal number of expected continuation bytes: 1
		asl
		bpl .start_sequence_X_Y_check	; handle 0xc0..0xdf (start of two-byte sequence)
		iny;ldy#2	minimal number of expected continuation bytes: 2
		asl
		bpl .start_sequence_X_Y	; handle 0xe0..0xef (start of three-byte sequence)
		iny;ldy#3	minimal number of expected continuation bytes: 3
		asl
		bpl .start_sequence_X_Y	; handle 0xf0..0xf7 (start of four-byte sequence)
		; definitions for five-byte and six-byte sequences have been
		; withdrawn, and $fe and $ff are forbidden as well, therefore:
		;FALLTHROUGH
.coding_error	lda .expected_bytes
		bmi .rts	; already ignoring? then keep ignoring
		jsr .error_out
		lda #$ff
		!by $2c
.init		lda #0
		sta .expected_bytes
.rts		rts

; number of continuation bytes to expect (ff/0/1/2/3, where $ff means infinity, i.e. error state)
.expected_bytes	!by 0
; byte sequence
.seq_length	!by 0	; will be 2/3/4 when calling out_sequence
.byte_sequence	!by 0, 0, 0, 0	; example: Euro sign would be $e2, $82, $ac, dontcare

.sixbits_X ; it's a continuation byte (passed in X)
		ldy .expected_bytes
		bmi .rts	; ignore -> keep ignoring
		beq .coding_error	; ok -> complain and start ignoring
		txa	; store byte in memory
		ldx .seq_length
		sta .byte_sequence, x
		inx
		stx .seq_length
		dec .expected_bytes
		bne .rts	; more to follow
		lda .byte_sequence
		jmp out_sequence_AX	; convert and output

.start_sequence_X_Y_check ; X is byte, Y is number of continuation bytes expected to follow
		cpx #$c2	; 0xc0 and 0xc1 are illegal, because they can only
		bcc .coding_error	; start code points that already fit in ascii!
.start_sequence_X_Y ; X is byte, Y is number of continuation bytes expected to follow
		stx .byte_sequence
		lda #1
		sta .seq_length
.new_state_Y ; Y is number of continuation bytes expected to follow
		lda .expected_bytes	; check old status
		sty .expected_bytes	; and set new one
		beq .rts	; ok -> ok
		bmi .rts	; ignore -> ok, we start a fresh sequence anyway
		; expecting -> show error because previous code point has not been finished
		; we expected continuation bytes, so we output the error char,
		; but we do not enter the "ignore" state because we already have
		; new valid data:
.error_out	lda #CHAR_CODING_ERROR
		jmp $ffd2

; handler for ASCII values (0x00..0x7f):
.ascii_AX ; value is passed in both A and X
		pha
		ldy #0	; new state (no continuation bytes)
		jsr .new_state_Y
		pla
		;jmp out_ascii_A
		;FALLTHROUGH
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; converter
out_ascii_A ; output code points in 0x00..0x7f range
		; 00..1f -> convert LF and CR
		; 41..5a -> add 0x80
		; 61..7a -> subtract 0x20
		cmp #$40
		bcc ._00_3f
		beq ._40
		cmp #$5b
		bcc ._41_5a
		cmp #$5e
		beq ._5e
		cmp #$61
		bcc .keep
		cmp #$7b
		bcc ._61_7a
		cmp #$7f
		bne .unsupported
		rts	; 7f is "ignore"

._00_3f		cmp #13	; CR -> ignore
		bne ++
			rts
++		cmp #10	; LF -> CR
		bne ++
			lda #13
++		ldx #0
		stx $f4	; make sure TABs are not quoted
		!by $2c
.unsupported	lda #CHAR_UNSUPPORTED
		!by $2c
._40		lda #$b0
		!by $2c
._41_5a		ora #$80
		!by $2c
._5e		lda #$bf
		!by $2c
._61_7a		and #$df
.keep		jmp $ffd2

.bigtable
;len2:
.£§		!h ad00000040
.´µ		!h afb1
.Ä		!h db
.Ö		!h dc
.Üßàâäèéêî	!h dd0000beb200b400bb000000aeacb5000000b6
.ôöùûü		!h b700bc0000b300b8bd
.Σ		!h ba
.π		!h de
;len3:
.↑		!h 5e
.√		!h b9
.─│		!h a600a1
.┌┐└┘├		!h a8000000aa000000a2000000a4000000a5
.┤		!h a7
.┬		!h a9
.┴		!h a3
.┼		!h ab

.show_len	txa	
		ora #$30	; now we output '2', '3' or '4'
		jmp $ffd2

out_sequence_AX ; output code points in 0x80..0x1fffff range, A is first byte, X is length of byte sequence (2/3/4)
		cpx #3
		beq .len3
		bcs .fail	; atm we do not support any code point with a 4-byte sequence
;len2:
		ldx #7;TABLE2_LENGTH - 1
.try2			lda .byte_sequence
			cmp .table2_1stbyte, x
			bne .next2
			lda .byte_sequence + 1
			cmp .table2_min, x
			bcc .next2
			cmp .table2_max, x
			bcc .ok2
			beq .ok2
.next2			dex
			bpl .try2
		; not found
.fail		lda #CHAR_UNSUPPORTED
		jmp $ffd2

.ok2		sec
		sbc .table2_min, x
		clc
		adc .table2_base, x
		tax
		lda .bigtable, x
		beq .fail
		jmp $ffd2

.table2_1stbyte	!h c2c2c3c3c3c3cecf
.table2_min	!h a3b484969cb4a380
.table2_max	!h a7b58496aebca380
.table2_base	!by .£§ - .bigtable, .´µ - .bigtable, .Ä - .bigtable, .Ö - .bigtable
		!by .Üßàâäèéêî - .bigtable, .ôöùûü - .bigtable, .Σ - .bigtable, .π - .bigtable

.len3		ldx #7;TABLE3_LENGTH - 1
.try3			lda .byte_sequence
			cmp .table3_1stbyte, x
			bne .next3
			lda .byte_sequence + 1
			cmp .table3_2ndbyte, x
			bne .next3
			lda .byte_sequence + 2
			cmp .table3_min, x
			bcc .next3
			cmp .table3_max, x
			bcc .ok3
			beq .ok3
.next3			dex
			bpl .try3
		; not found
		bmi .fail

.ok3		sec
		sbc .table3_min, x
		clc
		adc .table3_base, x
		tax
		lda .bigtable, x
		beq .fail
		jmp $ffd2

.table3_1stbyte	!h e2e2e2e2e2e2e2e2
.table3_2ndbyte	!h 8688949494949494
.table3_min	!h 919a808ca4acb4bc
.table3_max	!h 919a829ca4acb4bc
.table3_base	!by .↑ - .bigtable, .√ - .bigtable, .─│ - .bigtable, .┌┐└┘├ - .bigtable
		!by .┤ - .bigtable, .┬ - .bigtable, .┴ - .bigtable, .┼ - .bigtable
